import std::lp

data1 = embed_str("data/10-example.txt")
data2 = embed_str("data/10.txt")
data = data2

fn main() -> Void:
    total = 0
    for line in data.lines():
        buttons, joltages = ///
            "[" [.#]+ "]" (" " <button>)+, " {" <csv> "}"
            button: "(" <csv> ")"
            csv: <int> ("," <int>)*
            int: "0" | [1-9][0-9]* -> Int::parse
        ///.parse(line)

        presses_var = lp::LPInt::const(0)
        joltage_vars = Array::new(joltages.len(), |_| lp::LPInt::const(0))
        for i, button in buttons.enumerate():
            button_var = lp::LPInt::new("b${i}", min: 0)
            presses_var += button_var
            for j in button:
                joltage_vars[j] += button_var

        problem = lp::LPProblem::minimize(presses_var)
        for i, joltage in joltages.enumerate():
            problem.add_constraint(joltage_vars[i] == joltage)

        match problem.solve():
            Optimal(presses) => total += presses
            _ => unreachable
    print(total)
