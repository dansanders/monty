data1 = embed_str("data/10-example.txt")
data2 = embed_str("data/10.txt")
data = data2

fn parse_lights(lights: Str) -> Int:
    lights
        .find_all("#")
        .parse_button()

fn parse_button(button: Iter[Int]) -> Int:
    button
        .map(|i| 1 << i)
        .fold(0, Int::bit_or)

fn main() -> Void:
    total = 0
    for line in data.lines():
        target, buttons = ///
            "[" <lights> "]", (" " <button>)+ " {" csv "}"
            lights: [.#]+ -> parse_lights
            button: "(" <csv> ")" -> parse_button
            csv: <int> ("," <int>)*
            int: "0" | [1-9][0-9]* -> Int::parse
        ///.parse(line)

        states = Map[Int -> Int]()
        states[0] = 0
        for button in buttons:
            prev_states = states.copy()
            for prev_lights, prev_presses in prev_states:
                lights = prev_lights ^ button
                presses = prev_presses + 1
                match states.contains(lights):
                    true => states[lights] = min(states[lights], presses)
                    false => states[lights] = presses
        total += states[target]
    print(total)
